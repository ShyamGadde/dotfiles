# shellcheck shell=zsh

if [[ -f /usr/bin/pfetch ]]; then
    pfetch
fi

# History
export HISTFILE=~/.cache/zsh_history
export HISTSIZE=100000
export SAVEHIST=$HISTSIZE

# ZSH options
setopt noflowcontrol    # Disable Ctrl-S and Ctrl-Q output freezing
setopt prompt_subst     # Enable command substitution in prompt
setopt append_history   # This is default but set for share_history
setopt share_history    # Share history between all sessions
setopt extended_history # Save each command's beginning timestamp and duration

# ----------------
# Vi mode
# ----------------
bindkey -v
export KEYTIMEOUT=1 # The delay of pressing Esc key in vi mode is 0.4s by default, this makes it shorter (0.01s)

# Change cursor shape for different vi modes.
function zle-keymap-select {
    if [[ ${KEYMAP} == vicmd ]] || [[ $1 = 'block' ]]; then
        echo -ne '\e[1 q' # Block cursor
    elif [[ ${KEYMAP} == main ]] || [[ ${KEYMAP} == viins ]] || [[ ${KEYMAP} = '' ]] || [[ $1 = 'beam' ]]; then
        echo -ne '\e[5 q' # Beam cursor
    fi
}
zle -N zle-keymap-select

# Use beam shape cursor on startup.
echo -ne '\e[5 q'

# Use beam shape cursor for each new prompt.
precmd() {
    echo -ne '\e[5 q'
}

# ----------------
# Load Prompt
# ----------------
# NOTE: Load starship after enabling the vi mode.
eval "$(starship init zsh)"

# ----------------
# Load plugins
# ----------------
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d "$ZINIT_HOME" ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d "$ZINIT_HOME"/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

# FZF
export FZF_DEFAULT_COMMAND="fd --type f"
export FZF_DEFAULT_OPTS=" \
--border \
--reverse \
--color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8 \
--color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc \
--color=marker:#f5e0dc,fg+:#cdd6f4,prompt:#cba6f7,hl+:#f38ba8"
export FZF_CTRL_T_OPTS="
--walker-skip .git,node_modules,target
--height 50%
--preview 'bat -n --color=always {}'
--bind 'Alt-p:change-preview-window(down|hidden|)'"
export FZF_ALT_C_OPTS="
--walker-skip .git,node_modules,target
--height 50%
--preview 'eza --color=always --group-directories-first --icons -T {}'"

# Pyenv
export PYENV_ROOT="$HOME/.pyenv"
[[ -d $PYENV_ROOT/bin ]] && export PATH="$PYENV_ROOT/bin:$PATH"
export VIRTUAL_ENV_DISABLE_PROMPT=1

# NVM
# TODO: Check if completions are correctly loaded for zinit
export NVM_COMPLETION=true
export NVM_LAZY_LOAD=true
export NVM_LAZY_LOAD_EXTRA_COMMANDS=(
    'code'
    'vi'
    'vim'
    'nvim'
)

zinit --wait --lucid for \
    --atinit="zicompinit; zicdreplay" --atload="fast-theme XDG:catppuccin-mocha -q" \
    @zdharma-continuum/fast-syntax-highlighting \
    \
    --atload="_zsh_autosuggest_start" \
    @zsh-users/zsh-autosuggestions \
    \
    --blockf --atpull="zinit creinstall -q ." \
    --atload="source <(fzf --zsh)" \
    --atload="eval $(atuin init zsh --disable-up-arrow)" \
    @zsh-users/zsh-completions
# NOTE: FZF has to be loaded before atuin so that atuin can override the CTRL-R keybinding of FZF

zinit --wait=1 --lucid for \
    @hlissner/zsh-autopair \
    \
    --atload="eval $(pyenv init -)" \
    --atload="eval $(pyenv virtualenv-init -)" \
    --atload="eval $(zoxide init --cmd cd zsh)" \
    @lukechilds/zsh-nvm

# shellcheck disable=SC2046
zinit --wait=1 --lucid --link --is-snippet for \
    $(fd --type f . "$ZDOTDIR"/aliases | tr '\n' ' \ ') \
    \
    "$ZDOTDIR"/keybinds.zsh

# Arch Linux command-not-found support, you must have package pkgfile installed
# https://wiki.archlinux.org/index.php/Pkgfile#.22Command_not_found.22_hook
if [[ -e /usr/share/doc/pkgfile/command-not-found.zsh ]]; then
    zinit --wait=5 --lucid --link --is-snippet for \
        --atload="[[ -e /usr/share/doc/find-the-command/ftc.zsh ]] && source /usr/share/doc/find-the-command/ftc.zsh" \
        /usr/share/doc/pkgfile/command-not-found.zsh
fi
